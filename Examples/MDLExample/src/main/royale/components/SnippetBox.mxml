<?xml version="1.0" encoding="utf-8"?>
<!--

Licensed to the Apache Software Foundation (ASF) under one or more
contributor license agreements.  See the NOTICE file distributed with
this work for additional information regarding copyright ownership.
The ASF licenses this file to You under the Apache License, Version 2.0
(the "License"); you may not use this file except in compliance with
the License.  You may obtain a copy of the License at

http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.

-->
<js:Group xmlns:fx="http://ns.adobe.com/mxml/2009"
		  xmlns:js="library://ns.apache.org/royale/basic"
		  xmlns:mdl="library://ns.apache.org/royale/mdl"
		  xmlns:html="library://ns.apache.org/royale/html" 
		  initComplete="onSnippetBoxInitComplete(event)">
    <js:beads>
		<js:ContainerDataBinding />
	</js:beads>	
	<html:Pre localId="preCode">	
		<html:style>
			<js:AllCSSStyles margin="0" padding="0" fontSize="14px" 
			                 backgroundColor="#FFFFFF" overflow="auto"/>
		</html:style>
		<html:Code localId="snippetsCode" className="{languages}" text="{code}">
			<html:style>
				<js:AllCSSStyles padding="0" overflowX="unset"
								 backgroundColor="#FFFFFF"/>
			</html:style>
		</html:Code>
		<mdl:Spinner localId="spinner" isActive="false" width="45" height="45" />	
	</html:Pre>		
	<fx:Script>
	<![CDATA[
		import org.apache.royale.net.URLBinaryLoader;
		import org.apache.royale.net.URLRequest;
		import utils.Highlight;
		
		[Bindable]
		public var link:String;
		[Bindable]
		public var languages:String;
		[Bindable]
		private var code:String;
		private var _loader:URLBinaryLoader;
		
		private function onSnippetBoxInitComplete(event:Event):void
		{
			_loader = new URLBinaryLoader();
			_loader.addEventListener(Event.COMPLETE, onLoaderComplete);
			_loader.addEventListener("communicationError", onLoaderCommunicationError);
			
			//Set spinner in the center
			preCode.style = "textAlign:center;";
			spinner.visible = spinner.isActive = false;
			_loader.load(new URLRequest(link));
			
			snippetsCode.element["data-clipboard-target"] = "";
		}
		
		private function onLoaderComplete(event:Event):void
		{
			code = _loader.data.readUTFBytes(_loader.data.length);
			
			COMPILE::JS
			{
				var highlight:Highlight = new Highlight();
				highlight.highlightBlock(snippetsCode.element);
			}
			
			_loader.removeEventListener(Event.COMPLETE, onLoaderComplete);
			_loader.close();
			
			//Bring back text to the left
			preCode.style = "textAlign:left;";
			spinner.visible = spinner.isActive = false;
		}
		
		private function onLoaderCommunicationError(event:Event):void
		{
			_loader.removeEventListener("communicationError", onLoaderCommunicationError);
			_loader.close();
			
			preCode.style = "textAlign:left;";
			spinner.visible = spinner.isActive = false;
		}
	]]>
	</fx:Script>
</js:Group>